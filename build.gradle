plugins {
    id "com.diffplug.eclipse.apt" version "3.30.0"
    id "com.diffplug.spotless" version "5.14.1"
}

ext {
    springBootVersion = '2.5.2'
}

group 'com.pattycake.example'
version '1.0-SNAPSHOT'

subprojects {
    apply plugin: "java-library"
    apply plugin: "com.diffplug.eclipse.apt"
    apply plugin: "com.diffplug.spotless"
    
    repositories {
        mavenCentral()
    }

    sourceCompatibility = 11
    targetCompatibility = 11

    dependencies {
        implementation platform("org.springframework.boot:spring-boot-dependencies:${springBootVersion}")
        annotationProcessor platform("org.springframework.boot:spring-boot-dependencies:${springBootVersion}")
    }

    eclipse {
        classpath {
            file.beforeMerged { cp ->
                cp.entries.add( new org.gradle.plugins.ide.eclipse.model.SourceFolder('.apt_generated', null) )
            }
        }
    }

    spotless {
        java {
            importOrder()

            removeUnusedImports()

            googleJavaFormat() 
        }
    }
}
